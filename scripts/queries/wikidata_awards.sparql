# This is sort of impenetrible without some reference to what
# the various entities and relations mean
# I recommend pasting it into the online editor
# https://query.wikidata.org/
# which gives hover information for each entity and relation
SELECT DISTINCT 
    ?itemLabel
    (STRAFTER(STR(?item), "http://www.wikidata.org/entity/") AS ?work_qid) 
    ?authorLabel
    ?author_qid
    (YEAR(?date) AS ?year) 
    ?awardLabel 
    ?goodreads_id
    ?openlibrary_id
    ?isbn
    ?pubDate
    ?status 
    ?article
WHERE {
    VALUES (?prop ?ps ?status) {
        (p:P166 ps:P166 "winner")
        (p:P1411 ps:P1411 "nominated")
    }

    ?item ?prop ?stmt.
    ?stmt ?ps ?award;
        pq:P585 ?date.

    VALUES ?award {
        wd:Q255032 # Hugo Award
        wd:Q2576795 # Locus Award
        wd:Q266012 # Nebula Award
        wd:Q898527 # World Fantasy Award
        wd:Q1341487 # Philip K. Dick Award
        wd:Q1030402 # John W. Campbell Memorial Award
        wd:Q39060754 # Dragon Award
        wd:Q1666525 # International Fantasy Award
    }

    ?item wdt:P7937 wd:Q8261;
        wdt:P50 ?author;
        wdt:P577 ?workPubDate;
        wdt:P407 ?workLang. 

    OPTIONAL { ?item wdt:P8383 ?goodreads_id. }    # goodreads
    OPTIONAL { ?item wdt:P648 ?openlibrary_id. }       # openlibrary
    OPTIONAL {
        ?item wdt:P747 ?edition.
        ?edition wdt:P407 wd:Q1860; # English
            (wdt:P957|wdt:P212) ?isbn;
            wdt:P577 ?engPubDate.
    }

    BIND(IF(?workLang = wd:Q1860, COALESCE(?workPubDate, ?engPubDate), ?engPubDate) AS ?pubDate)
    BIND(STRAFTER(STR(?author), "http://www.wikidata.org/entity/") AS ?author_qid)

      
    # Get wikipedia page
    OPTIONAL {
        ?article schema:about ?item ;
                 schema:isPartOf <https://en.wikipedia.org/> .
    }

    SERVICE wikibase:label {
        bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".
    }
}