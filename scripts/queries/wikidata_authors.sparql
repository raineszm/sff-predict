# Query biographical data for nominees
SELECT DISTINCT 
    (STRAFTER(STR(?author), "http://www.wikidata.org/entity/") AS ?author_qid)
    ?authorLabel
    ?genderLabel
    ?dob
    ?birthCountryLabel
WHERE {

          
    VALUES ?award {
        wd:Q255032 # Hugo Award
        wd:Q2576795 # Locus Award
        wd:Q266012 # Nebula Award
        wd:Q898527 # World Fantasy Award
        wd:Q1341487 # Philip K. Dick Award
        wd:Q1030402 # John W. Campbell Memorial Award
        wd:Q39060754 # Dragon Award
        wd:Q1666525 # International Fantasy Award
    }
  
    ?item (wdt:P1411|wdt:P166) ?award.
          
    ?item wdt:P7937 wd:Q8261;
          wdt:P50 ?author.
  
    OPTIONAL { ?author wdt:P21 ?gender. }
    OPTIONAL { ?author wdt:P569 ?dob. }
    OPTIONAL { ?author wdt:P19/wdt:P17 ?birthCountry. }
    OPTIONAL { ?author wdt:P27 ?residenceStmt. }
 


    SERVICE wikibase:label {
        bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".
    }
} GROUP BY   
    ?author
    ?authorLabel
    ?genderLabel
    ?dob
    ?birthCountryLabel